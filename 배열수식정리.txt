배열수식
sumif vs. sumproduct
=SUMIF(D7:D17,4,C7:C17) 월 컬럼 추가 필요
=SUMPRODUCT(--(MONTH(F7:F17)=4)*(G7:G17)) Enter

=SUMPRODUCT(C3:C11,D3:D11)
=SUMPRODUCT(--(ISNUMBER(SEARCH("쉐이크",B3:B11))),C3:C11)
=SUMPRODUCT(--ISNUMBER(SEARCH("쉐이크",B3:B11)), C3:C11, D3


https://platform.samsungcic.com/#/me/lms

1차시 처음 만나는 배열수식 쉽게 이해하기
입력 후 / 수정 후 Ctrl + Shift + Enter
배열수식 조건
AND 연산 : *
OR 연산 : +
논리값(결과값)

다중셀 : =B2:B11*C2:C11
단일셀 : =SUM(B2:B11*C2:C11)

2차시 행마다 수식 계산 후 총합 구하기, 중간 과정 없이 한번에 하려면?
문자수 : =SUM(LEN(A2:A10))
매출자료 : =SUM(IFERROR(D2:D101,0))
배열 수식이란?
내가 잡은 배열(집합)만큼 반복적으로 계산할 수 있도록 만들어 주는 것

3차시 수식 하나로 변화량의 합계, 평균, 최대값을 구하려면?
=AVERAGE(C2:C51-B2:B51)
=SUM(C2:C51-B2:B51)
=MAX(C2:C51-B2:B51)

4차시 범위 안에 특정 데이터가 존재하는지 확인하려면?
=IF(OR(B4:F21=C2),"있음","없음")

5차시 제품별 최대 매출량과 매출액을 한번에 구하려면?
=MAX(($B$2:$B$151=G2)*($E$2:$E$151))
* : AND 연산 0, 1
피벗으로도 구현 가능

6차시 다양한 조건에 맞는 합계를 쉽게 구하려면?
=SUM((지역=$C$3)*(구분=$E4)*(회원구분=F$3)*(운임총액))
이름정의 필요 
and 연산 *   0, 1
SUMIFS 함수로도 가능

7차시 양이 많은 데이터에서 월별 합계를 빠르게 구하려면?
=MONTH(날짜) → 월
=SUM((MONTH(일자)=H4)*(금액))
SUMIFS 함수 불가능 (함수 포함x)

8차시 두 데이터를 비교하여 같은 값과 다른 값이 몇 개인지 찾으려면?
이름 전자료, 후자료
값이 같은 개수
=SUM((전자료=후자료)*1)
=SUM((전자료<>후자료)*1)

9차시 데이터에서 텍스트 혹은 에러가 입력된 셀만 세려면?
=COUNTA(범위) : 범위 안에 비어있지 않은 셀의 수
=COUNT(범위) : 범위 안에 숫자가 입력된 셀의 수

=ISTEXT(값) : 문자면 TRUE, 아니면 FALSE
=ISERROR(값) : 에러면 TRUE, 아니면 FALSE

데이터가 입력된 셀의 수 : =COUNTA(A1:F20)
숫자가 입력된 셀의 수 : =COUNT(A1:F20)
문자의 입력된 셀의 수 : =SUM(ISTEXT(A1:F20)*1) 배열함수
에러가 입력된 셀의 수 : =SUM(ISERROR(A1:F20)*1) 배열함수

10차시 제품별 최소 매출량과 매출액을 구하려면?
MIN, IF 함수 필요
MIN((제품명=G2)*,매출량) → 0, 제품명=G2 FALSE일 경우 0, 최소값 0
=MIN(IF(제품명=G2,매출량,FALSE))

11차시 상위 분류를 기준으로 합계와 개수를 구하려면?
- 쌀    : 백미, 현미, 흑미, 찹쌀	
- 잡곡 : 보리, 귀리, 기장, 녹두, 수수	
금액
=SUM(((제품="백미")+(제품="현미")+(제품="흑미")+(제품="찹쌀"))*(금액))
=SUM(((제품="보리")+(제품="귀리")+(제품="기장")+(제품="녹두")+(제품="수수"))*(금액))
거래건수
=SUM(((제품="백미")+(제품="현미")+(제품="흑미")+(제품="찹쌀"))*1)
=SUM(((제품="보리")+(제품="귀리")+(제품="기장")+(제품="녹두")+(제품="수수"))*1)

12차시 범위 내 상위 또는 하위 세 개의 값만 합산하려면?
LARGE(범위, 숫자)
SMALL(범위, 숫자)
=SUM(LARGE(G2:G31,{1,2,3}))
=SUM(SMALL(G2:G31,{1,2,3}))

13차시 내역서에서 월별 누계를 자동으로 구하려면?
SUM
MONTH
$A$1
A1
=SUM((MONTH($A$2:A62)=MONTH(A62))*($D$2:D62))



 




